plugins {
	id 'java-library'
	id "de.undercouch.download" version "4.1.1"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(8)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'com.ongres:fluent-process:1.0.1'
	implementation 'com.google.code.gson:gson:2.8.6'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
	testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
	testImplementation 'org.assertj:assertj-core:3.18.1'
	testImplementation 'com.google.guava:guava:30.1-jre'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent
tasks.withType(Test) { testTask ->
	useJUnitPlatform()

	testTask.testLogging { logging ->
		events TestLogEvent.FAILED,
				TestLogEvent.SKIPPED,
				TestLogEvent.STANDARD_OUT,
				TestLogEvent.STANDARD_ERROR

		exceptionFormat TestExceptionFormat.FULL
		showExceptions true
		showCauses true
		showStackTraces true
	}
}

task downloadOnePassword {
	doLast {
		def onePasswordVersion = file("${sourceSets.test.resources.srcDirs.first()}/op.version").text
		def buildBinDir = file("$buildDir/bin")
		download {
			src ([
				"https://cache.agilebits.com/dist/1P/op/pkg/v${onePasswordVersion}/op_linux_amd64_v${onePasswordVersion}.zip",
				"https://cache.agilebits.com/dist/1P/op/pkg/v${onePasswordVersion}/op_windows_amd64_v${onePasswordVersion}.zip"
			])
			dest buildBinDir
			overwrite false
		}

		fileTree(dir: buildBinDir).include("op_*_v${onePasswordVersion}.zip").each { zip ->
			copy {
				from zipTree(zip)
				into buildBinDir
			}
		}
	}
}

task bootstrap(dependsOn: ["downloadOnePassword"])
processTestResources.dependsOn += ["downloadOnePassword"]