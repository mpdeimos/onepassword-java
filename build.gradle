plugins {
	id 'java-library'
	id 'jacoco'
	id "de.undercouch.download" version "4.1.1"
	id 'com.adarshr.test-logger' version '2.1.1'
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(8)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'com.ongres:fluent-process:1.0.1'
	implementation 'com.google.code.gson:gson:2.8.6'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
	testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
	testImplementation 'org.assertj:assertj-core:3.18.1'
	testImplementation 'com.google.guava:guava:30.1-jre'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

tasks.withType(Test) {
	useJUnitPlatform()
	testlogger {
		showPassedStandardStreams false
		slowThreshold 5000
	}
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	reports {
		xml.enabled true
	}
}

task downloadBinaries {
	doLast {
		def onePasswordVersion = file("${sourceSets.test.resources.srcDirs.first()}/op.version").text
		def teamscaleUploadVersion = "2.0.0"
		def buildBinDir = file("$buildDir/bin")
		def os = "linux"
		if (System.properties['os.name'].toLowerCase().contains('windows')) {
			os = "windows"
		}
		download {
			src ([
				"https://cache.agilebits.com/dist/1P/op/pkg/v${onePasswordVersion}/op_${os}_amd64_v${onePasswordVersion}.zip",
				"https://github.com/cqse/teamscale-upload/releases/download/v${teamscaleUploadVersion}/teamscale-upload-${os}.zip",
			])
			dest buildBinDir
			overwrite false
		}

		fileTree(dir: buildBinDir).include("op_${os}_amd64_v${onePasswordVersion}.zip").include("teamscale-upload-${os}.zip").each { zip ->
			copy {
				from zipTree(zip)
				into buildBinDir
			}
		}

		if (os == 'linux') {
			project.exec {
				commandLine('chmod',  '+x', "$buildBinDir/teamscale-upload")
			}
		}
	}
}

task bootstrap(dependsOn: ["downloadBinaries"])
processTestResources.dependsOn += ["downloadBinaries"]